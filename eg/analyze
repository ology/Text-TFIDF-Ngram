#!/usr/bin/env perl
use strict;
use warnings;

use File::Find::Rule;
use Text::TFIDF::Ngram;

my $dir    = shift || die "Usage: perl $0 /some/corpus size top stopwords phrase";
my $size   = shift || 2;
my $top    = shift // 0;
my $stop   = shift // 1;
my $phrase = shift || '';

my @files = File::Find::Rule->file()->name('*.txt')->in($dir);

my $t = Text::TFIDF::Ngram->new(
    files     => \@files,
    size      => $size,
    stopwords => $stop,
);

# If the top phrases is requested...
if ( $top ) {
    my $x = $t->tfidf_by_file();

    my $i = 0;

    for my $file ( sort keys %$x ) {
        $i++;

        printf "%d. %s\n", $i, $file;

        my $n = 0;

        for my $p ( sort { $x->{$file}{$b} <=> $x->{$file}{$a} } keys %{ $x->{$file} } ) {
            last if ++$n > $top;

            printf "\t%d. %s = %.10f\n", $n, $p, $x->{$file}{$p};
        }
    }
}

# If an analysis of a single phrase is requested...
if ( $phrase ) {
    printf "Phrase: '%s', IDF = %.10f\n", $phrase, $t->idf($phrase);

    my $i = 0;
    for my $file ( @{ $t->files } ) {
        my $tf = $t->tf( $file, $phrase );
        if ( $tf ) {
            $i++;

            printf "%d. %s\n", $i, $file;

            printf "\tTF = %.10f\n", $t->tf( $file, $phrase );

            my $tfidf = $t->tfidf( $file, $phrase );
            printf "\tTF-IDF = %.10f\n", $tfidf
                if $tfidf;
        }
    }
}

__END__
!perl % ~/Documents/lit/inaugural/ 2 5
!perl % ~/Documents/lit/inaugural/ 2 0 1 'public good'
