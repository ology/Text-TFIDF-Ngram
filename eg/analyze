#!/usr/bin/env perl
use strict;
use warnings;

use File::Find::Rule;
use Text::TFIDF::Ngram;

my $usage = "Usage: perl $0 /some/corpus size top phrase";

my $dir  = shift || die $usage;
my $size = shift || 2;
my $top  = shift || 0;
my $word = shift || '';

my @files = File::Find::Rule->file()->name('*.txt')->in($dir);

my $t = Text::TFIDF::Ngram->new(
    files     => \@files,
    size      => $size,
    stopwords => 1,
);

if ( $top ) {
    my $x = $t->tfidf_by_file();

    my $i = 0;
    for my $file ( sort keys %$x ) {
        $i++;

        print "$i. File: $file\n";

        my $n = 0;
        for my $word ( sort { $x->{$file}{$b} <=> $x->{$file}{$a} } keys %{ $x->{$file} } ) {
            last if ++$n > $top;
            print "\t$n. $word = ", $x->{$file}{$word}, "\n";
        }
    }
}

if ( $word ) {
    print "Word: '$word' IDF: ", $t->idf($word), "\n";

    for my $file ( @{ $t->files } ) {
        print "File: $file\n";

        print "\tTF: ", $t->tf( $file, $word ), "\n";

        my $tfidf = $t->tfidf( $file, $word );
        print "\tTF-IDF: $tfidf\n" if $tfidf;
    }
}

__END__
!perl % ~/Documents/lit/inaugural/ 2 5
!perl % ~/Documents/lit/inaugural/ 2 0 'public good'
